import org.palladiosimulator.pcm.repository.RepositoryPackage

import "http://www.emftext.org/java" as java
import "http://palladiosimulator.org/PalladioComponentModel/5.2" as pcm


reactions: commonInterface
in reaction to changes in AdjustedJava
execute actions in PCMExtended


routine createInterface(java::ConcreteClassifier javaInterface) {
	match {
		val pcmRepository = retrieve pcm::Repository corresponding to RepositoryPackage.Literals.REPOSITORY
		require absence of pcm::OperationInterface corresponding to javaInterface
	}
	action {
		val pcmInterface = create pcm::OperationInterface and initialize {
			pcmInterface.entityName = javaInterface.name
		}
		update pcmRepository {
			pcmRepository.interfaces__Repository += pcmInterface
		}
		call {
			addCorrespondenceBetweenClassifierAndInterface(javaInterface, pcmInterface)
		}
	}
}

routine addCorrespondenceBetweenClassifierAndInterface(java::ConcreteClassifier javaClassifier, pcm::Interface pcmInterface) {
	action {
		add correspondence between javaClassifier and pcmInterface
	}
}

routine connectInterfacesByInheritance(java::ConcreteClassifier subClassifier, java::ConcreteClassifier superClassifier) {
	match {
		val subInterface = retrieve pcm::OperationInterface corresponding to subClassifier
		val superInterface = retrieve pcm::OperationInterface corresponding to superClassifier
	}
	action {
		update subInterface {
			subInterface.parentInterfaces__Interface += superInterface
		}
	}
}

routine removeInterface(java::Classifier classifier) {
	match {
		val opInterface = retrieve pcm::OperationInterface corresponding to classifier
	}
	action {
		delete opInterface
	}
}
