import org.palladiosimulator.pcm.repository.RepositoryPackage

import "http://www.emftext.org/java" as java
import "http://palladiosimulator.org/PalladioComponentModel/5.2" as pcm

reactions: commonInterface
in reaction to changes in java
execute actions in pcm

routine createInterface(java::ConcreteClassifier javaInterface) {
	match {
		val pcmRepository = retrieve pcm::Repository corresponding to RepositoryPackage.Literals.REPOSITORY
		require absence of pcm::OperationInterface corresponding to javaInterface
	}
	create {
		val pcmInterface = new pcm::OperationInterface
	}
	update {
		pcmInterface.entityName = javaInterface.name
		pcmRepository.interfaces__Repository += pcmInterface
		addCorrespondenceBetweenClassifierAndInterface(javaInterface, pcmInterface)
	}
}

routine addCorrespondenceBetweenClassifierAndInterface(java::ConcreteClassifier javaClassifier, pcm::Interface pcmInterface) {
	update {
		addCorrespondenceBetween(javaClassifier, pcmInterface)
	}
}

routine connectInterfacesByInheritance(java::ConcreteClassifier subClassifier, java::ConcreteClassifier superClassifier) {
	match {
		val subInterface = retrieve pcm::OperationInterface corresponding to subClassifier
		val superInterface = retrieve pcm::OperationInterface corresponding to superClassifier
	}
	update {
		subInterface.parentInterfaces__Interface += superInterface
	}
}

routine removeInterface(java::Classifier classifier) {
match {
	val opInterface = retrieve pcm::OperationInterface corresponding to classifier
}
	update {
		removeObject(opInterface)
	}
}
